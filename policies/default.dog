//! trying to work only with the "raw" field, as we might want to ditch all processing in elfsign itself

/// A signed binary
pattern signed-binary = {
    certificate-bundle: certificate-bundle,
}

/// The certificate bundle. Allow self-signed or signed by a trust root
pattern certificate-bundle = self-signed || root-signed

/// Self-signed, exactly one certificate, where the issuer signed the subject itself.
pattern self-signed = {
    raw: [
        x509::DER(common-certificate)
    ]
}

/// Signed by one of the trusted roots (from trusted-root)
pattern root-signed = {
    raw: list::Head({
        head: [leaf],
        main: list::Tail({
          main: list::All<intermediate>,
          tail: [root],
        }),
    })
}

/// Common certificate checks
pattern common-certificate = {
    version: 2,
}

pattern leaf = x509::DER(leaf-certificate)
pattern leaf-certificate = common-certificate && {
    extensions:
        list::Any<{keyUsage: list::Any<"Digital Signature">}>
        &&
        list::Any<{
            extendedKeyUsage: {
                any: true,
            } || {
                codeSigning: true,
            }
        }>
        &&
        list::None<{CA: true}>
}

pattern intermediate = x509::DER(intermediate-certificate)
pattern intermediate-certificate = common-certificate && {
    extensions: list::Any<{
      CA: true
    }>
}

pattern root = pem::AsCertificate(trusted-root) && x509::DER(root-certificate)
pattern root-certificate = common-certificate && {
    extensions: list::Any<{
      CA: true
    }>
}

pattern trusted-root = sigstore-root // || some-other-root

// fulcio root cert
pattern sigstore-root = "-----BEGIN CERTIFICATE-----
MIIB9zCCAXygAwIBAgIUALZNAPFdxHPwjeDloDwyYChAO/4wCgYIKoZIzj0EAwMw
KjEVMBMGA1UEChMMc2lnc3RvcmUuZGV2MREwDwYDVQQDEwhzaWdzdG9yZTAeFw0y
MTEwMDcxMzU2NTlaFw0zMTEwMDUxMzU2NThaMCoxFTATBgNVBAoTDHNpZ3N0b3Jl
LmRldjERMA8GA1UEAxMIc2lnc3RvcmUwdjAQBgcqhkjOPQIBBgUrgQQAIgNiAAT7
XeFT4rb3PQGwS4IajtLk3/OlnpgangaBclYpsYBr5i+4ynB07ceb3LP0OIOZdxex
X69c5iVuyJRQ+Hz05yi+UF3uBWAlHpiS5sh0+H2GHE7SXrk1EC5m1Tr19L9gg92j
YzBhMA4GA1UdDwEB/wQEAwIBBjAPBgNVHRMBAf8EBTADAQH/MB0GA1UdDgQWBBRY
wB5fkUWlZql6zJChkyLQKsXF+jAfBgNVHSMEGDAWgBRYwB5fkUWlZql6zJChkyLQ
KsXF+jAKBggqhkjOPQQDAwNpADBmAjEAj1nHeXZp+13NWBNa+EDsDP8G1WWg1tCM
WP/WHPqpaVo0jhsweNFZgSs0eE7wYI4qAjEA2WB9ot98sIkoF3vZYdd3/VtWB5b9
TNMea7Ix/stJ5TfcLLeABLE4BNJOsQ4vnBHJ
-----END CERTIFICATE-----
"
